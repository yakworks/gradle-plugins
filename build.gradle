// First, apply the publishing plugin
buildscript {
  repositories {
    mavenLocal()        // for downstream test
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'org.shipkit:shipkit:2.0.22'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:+'
    classpath 'io.spring.gradle:dependency-lock:latest.release'
  }
}
plugins {
  id "com.dorongold.task-tree" version "1.3"
}
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'org.shipkit.gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'spring.lock' //run gradle updateLocks

group = 'org.yakworks'

repositories {
  jcenter()
  maven { url "https://plugins.gradle.org/m2/" }
}

//run `gradle updateLocks` to update the locks below to highest version when using the + notation
dependencies {
  // gradle
  compile gradleApi()
  compile localGroovy()

  runtime 'org.ajoberstar:grgit:+' lock '2.2.0'
  runtime 'org.ajoberstar:gradle-git-publish:+' lock '0.4.0'
  // 3rd party plugins
  runtime 'com.diffplug.spotless:spotless-plugin-gradle:+' lock '3.10.0'
  runtime "gradle.plugin.com.energizedwork:idea-gradle-plugins:+" lock '1.4'
  runtime "gradle.plugin.com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.2.0"
  compile 'org.shipkit:shipkit:2.+' lock '2.0.22'
  compile 'org.yaml:snakeyaml:1.+' lock '1.19'
  //runtime 'com.netflix.nebula:gradle-lint-plugin:+'
  compile 'net.sf.corn:corn-cps:1.0.1'

  testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
    exclude module: "groovy-all"
  }
  testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'    //automatically unroll Spock tests (if appropriate)
  testCompile "net.bytebuddy:byte-buddy:1.7.1"
  testCompile "org.objenesis:objenesis:2.5"
  testCompile "commons-lang:commons-lang:2.4"
  testCompile gradleTestKit()
}
def defTags = ['grails', 'groovy', 'shipkit', 'spotless', 'circleCI', 'continuous delivery', 'release', 'defaults', 'codenarc', 'mkdocs', 'markdown', 'documentation']
pluginBundle {
  website = 'https://github.com/yakworks/gradle-plugins'
  vcsUrl = 'https://github.com/yakworks/gradle-plugins.git'
  description = 'Plugin providing opinionated defaults and shipkit setup for Groovy, Grails and Gradle library/plugin projects. Enables hooks for automated releases and versioning and tags with CircleCI. '
  tags = defTags
  plugins {
    publishPlugin {
      id = 'yakworks.defaults'
      displayName = 'Yakworks defaults used for our Groovy, Grails and Gradle projects. Setup defauls for codenarc, spotless '
      tags = defTags
    }
    publishPlugin {
      id = 'yakworks.grails-plugin'
      displayName = 'Marks and sets up defaults and publishing (shipkit) for grails plugins. applies shipkit and circleCI settings '
      tags = defTags
    }
    publishPlugin {
      id = 'yakworks.shipkit'
      displayName = 'Wrapper around shipkit so it works without a non-bintray maven repo susch as artifactory, integrates with circleCI, adds Grails support and generates mkdocs'
      tags = defTags
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}

wrapper {
  gradleVersion = '4.6'
}

// spotless.formats.groovy.target 'src/main/groovy/**/*.groovy'
// gradleLint.excludedRules = ['transitive-duplicate-dependency-class']

tasks.create('foo'){
  doLast{
    println "GOT IT"
  }
}
